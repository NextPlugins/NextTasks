plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.0.1'

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    maven { url = 'https://repo.codemc.org/repository/maven-public' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'

    implementation 'org.quartz-scheduler:quartz:2.3.2'
    implementation 'com.github.HenryFabio.configuration-injector:bukkit:main-SNAPSHOT'
    implementation 'com.github.SaiintBrisson.command-framework:bukkit:1.2.0'

    def lombok = 'org.projectlombok:lombok:1.18.16'
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.tasks.NextTasks'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")
    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.tasks.libs.configinjector'
    relocate 'me.saiintbrisson.minecraft.command', 'com.nextplugins.tasks.libs.command.common'
    relocate 'me.saiintbrisson.bukkit.command', 'com.nextplugins.tasks.libs.command.bukkit'
    relocate 'com.mchange', 'com.nextplugins.tasks.libs.quartz'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.tasks.libs.hikari'
}

compileJava {
    options.encoding = 'UTF-8'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}